import com.modrinth.minotaur.dependencies.DependencyType
import com.modrinth.minotaur.dependencies.ModDependency
import net.darkhax.curseforgegradle.Constants as CFConstants
import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
}

base {
	archivesName = "$mod_name-$mc_version-fabric"
}

println "Fabric API: $fabric_version, Loader: $loader_version"

loom {
	runs {
		configureEach {
			ideConfigGenerated(true)
			runDir('../run')
		}

		client {
			client()

			setConfigName('Fabric: Client')
		}
	}

	mixin {
		useLegacyMixinAp = false
	}

	accessWidenerPath.set(project(':Common').file("src/main/resources/${mod_id}.accesswidener"))
}

repositories {
	maven { url 'https://maven.minecraftforge.net/' }
	maven { url 'https://maven.parchmentmc.org' }
}

dependencies {
	minecraft "com.mojang:minecraft:$mc_version"

	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-$mc_version:$mappings_version@zip")
	}

	compileOnly project(':Common')

	modImplementation "net.fabricmc:fabric-loader:$loader_version"
	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

	compileOnly "org.projectlombok:lombok:$lombok_version"
	annotationProcessor "org.projectlombok:lombok:$lombok_version"

	implementation 'com.google.code.findbugs:jsr305:3.+'
	implementation 'org.jetbrains:annotations:24.+'
}

sourcesJar {
	from project(':Common').sourceSets.main.allSource
}

artifacts {
	archives remapJar
	archives sourcesJar
}

project.evaluationDependsOn(':Changelog')

tasks.register('publishCurseForge', TaskPublishCurseForge) {
	dependsOn(tasks.remapJar)
	dependsOn(':Changelog:makeHtmlChangelog')

	apiToken = System.getenv('CURSEFORGE_TOKEN')

	var modFile = upload('937011', tasks.remapJar.archiveFile)
	modFile.displayName = "${base.archivesName.get()}-$version"
	modFile.releaseType = CFConstants.RELEASE_TYPE_BETA
	modFile.addGameVersion(mc_version, 'client')
	modFile.addModLoader 'Fabric'
	modFile.addJavaVersion "Java $java_version"
	modFile.addRequirement 'fabric-api'

	modFile.changelogType = CFConstants.CHANGELOG_HTML
	modFile.changelog = file("${project(':Changelog').projectDir}/changelog.html").getText().replace([ '${version}': "$mc_version-$version" ])
}

modrinth {
	token = System.getenv('MODRINTH_TOKEN')

	projectId = 'fbp-renewed'

	versionName = "${base.archivesName.get()}-$version"
	versionNumber = "$version+fabric"
	versionType = 'beta'

	gameVersions = [ mc_version ]
	loaders = [ 'fabric' ]

	dependencies = [
			new ModDependency('P7dR8mSH', DependencyType.REQUIRED)
	]

	changelog = provider { file("${project(':Changelog').projectDir}/changelog.md").getText().replace([ '${version}': "$mc_version-$version" ]) }

	uploadFile = tasks.remapJar
	additionalFiles = [ tasks.sourcesJar ]
}

tasks.modrinth.dependsOn(tasks.remapJar)
tasks.modrinth.dependsOn(tasks.sourcesJar)

tasks.modrinth.dependsOn(':Changelog:makeMarkdownChangelog')

publishing {
	publications {
		register('mavenJava', MavenPublication) {
			artifact remapJar
			artifact sourcesJar

			artifactId archivesBaseName
		}
	}

	repositories {
		mavenLocal()
	}
}
